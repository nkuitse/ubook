#!/bin/zsh -e

usage() {
    print "usage: urenum [-G] [-n] LOGIN UID" >&2
    exit 1
}

fatal() {
    print "fatal: $*"
    exit 2
}

typeset grouptoo=true run=zsh opt
while getopts :Gn opt; do
    case $opt in
        (G) grouptoo=false ;;
        (n) run=cat ;;
        (*) usage ;;
    esac
done
shift $(( OPTIND - 1 ))

(( $# == 2 )) || usage

typeset login=$1
integer olduid=$(id -u $login)
integer newuid=$2
typeset oldgrp=$(id -ng $login)
integer oldgid=$(id -g $login)
integer newgid=$newuid

(( oldgrp == login )) || ! $grouptoo || fatal "Group name and login are not the same"
(( olduid != 0 && newuid != 0 && oldgid != 0 && newgid != 0 )) || fatal "I won\'t touch root or wheel!"

$run >&2 <<EOS
pkill -u $login
pkill -G $oldgrp
/usr/sbin/usermod -u $newuid $login
find / -user $olduid -exec chown -h $login '{}' ';'

EOS

$grouptoo || exit 0

$run >&2 <<EOS
/usr/sbin/groupmod -g $newgid $login
/usr/sbin/usermod -g $newgid $login
find / -group $oldgid -exec chgrp -h $login '{}' ';'

EOS
